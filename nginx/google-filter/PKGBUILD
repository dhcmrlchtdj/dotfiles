pkgname=nginx-mainline-custom
pkgver=1.11.1
pkgrel=2
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server, mainline release'
arch=('i686' 'x86_64')
url='http://nginx.org'
license=('custom')
depends=('pcre' 'zlib' 'openssl' 'geoip')
backup=('etc/nginx/fastcgi.conf'
'etc/nginx/fastcgi_params'
'etc/nginx/koi-win'
'etc/nginx/koi-utf'
'etc/nginx/mime.types'
'etc/nginx/nginx.conf'
'etc/nginx/scgi_params'
'etc/nginx/uwsgi_params'
'etc/nginx/win-utf'
'etc/logrotate.d/nginx')
install=nginx.install
provides=('nginx')
conflicts=('nginx')
source=($url/download/nginx-$pkgver.tar.gz
service
logrotate
headers-more-nginx-module-0.30.tar.gz::https://github.com/openresty/headers-more-nginx-module/archive/v0.30.tar.gz
echo-nginx-module-0.59.tar.gz::https://github.com/openresty/echo-nginx-module/archive/v0.59.tar.gz
ngx_http_substitutions_filter_module-0.6.4.tar.gz::https://github.com/yaoweibin/ngx_http_substitutions_filter_module/archive/v0.6.4.tar.gz
ngx_http_google_filter_module-0.2.0.tar.gz::https://github.com/cuber/ngx_http_google_filter_module/archive/0.2.0.tar.gz)
md5sums=('0f3900165b11c417854535f3538913cb'
'ce9a06bcaf66ec4a3c4eb59b636e0dfd'
'3441ce77cdd1aab6f0ab7e212698a8a7'
'a223ba5180ec796a23686962e5732ff8'
'107b7a312927242cdf812389f3f0787d'
'bc4482c8f9a10a59d14e46693b87e00c'
'f8d104c44136fbd5f404018268f56419')

_modules=(
--add-module=../headers-more-nginx-module-0.30
--add-module=../echo-nginx-module-0.59
--add-module=../ngx_http_substitutions_filter_module-0.6.4
--add-module=../ngx_http_google_filter_module-0.2.0
)

_common_flags=(
--with-debug
# --with-ipv6
--with-pcre-jit
--with-file-aio
--with-http_addition_module
--with-http_auth_request_module
--with-http_dav_module
--with-http_degradation_module
--with-http_flv_module
--with-http_geoip_module
--with-http_gunzip_module
--with-http_gzip_static_module
--with-http_mp4_module
--with-http_realip_module
--with-http_secure_link_module
--with-http_ssl_module
--with-http_stub_status_module
--with-http_sub_module
--with-http_v2_module
--with-mail
--with-mail_ssl_module
--with-stream
--with-stream_ssl_module
--with-threads
)

_mainline_flags=(
)

build() {
	cd $provides-$pkgver
	./configure \
		--prefix=/etc/nginx \
		--conf-path=/etc/nginx/nginx.conf \
		--sbin-path=/usr/bin/nginx \
		--pid-path=/run/nginx.pid \
		--lock-path=/run/lock/nginx.lock \
		--user=http \
		--group=http \
		--http-log-path=/var/log/nginx/access.log \
		--error-log-path=stderr \
		--http-client-body-temp-path=/var/lib/nginx/client-body \
		--http-proxy-temp-path=/var/lib/nginx/proxy \
		--http-fastcgi-temp-path=/var/lib/nginx/fastcgi \
		--http-scgi-temp-path=/var/lib/nginx/scgi \
		--http-uwsgi-temp-path=/var/lib/nginx/uwsgi \
		${_modules[@]} \
		${_common_flags[@]} \
		${_mainline_flags[@]}

	make
}

package() {
	cd $provides-$pkgver
	make DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
		-e '44s|html|/usr/share/nginx/html|' \
		-e '54s|html|/usr/share/nginx/html|' \
		-i "$pkgdir"/etc/nginx/nginx.conf

	rm "$pkgdir"/etc/nginx/*.default

	install -d "$pkgdir"/var/lib/nginx
	install -dm700 "$pkgdir"/var/lib/nginx/proxy

	chmod 750 "$pkgdir"/var/log/nginx
	chown http:log "$pkgdir"/var/log/nginx

	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -Dm644 ../logrotate "$pkgdir"/etc/logrotate.d/nginx
	install -Dm644 ../service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$provides/LICENSE

	rmdir "$pkgdir"/run

	install -d "$pkgdir"/usr/share/man/man8/
	gzip -9c man/nginx.8 > "$pkgdir"/usr/share/man/man8/nginx.8.gz
}
