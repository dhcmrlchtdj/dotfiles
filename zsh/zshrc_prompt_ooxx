# https://github.com/sindresorhus/pure
# https://github.com/mathiasbynens/dotfiles/

prompt_ooxx_git_dirty() {
    if [[ ("$(command git rev-parse --is-inside-work-tree)" == "true") &&
        ("$(command git status --porcelain)" != "") ]]; then
        echo '*'
    fi
}

prompt_ooxx_git() {
    if [[ $vcs_info_msg_0_ != "" ]]; then
        echo "on %F{green}$vcs_info_msg_0_%f$(prompt_ooxx_git_dirty) "
    fi
}

prompt_ooxx_pyvenv() {
    if [[ $VIRTUAL_ENV != "" ]]; then
        echo "use %F{magenta}$(basename $VIRTUAL_ENV)%f "
    fi
}

prompt_ooxx_nvm() {
    if [[ $NVM_BIN != "" ]]; then
        echo "use %F{magenta}$(basename $(dirname $NVM_BIN))%f "
    fi
}

prompt_ooxx_precmd() {
    setopt prompt_subst

    (( ${+functions[vcs_info]} )) && vcs_info

    p0=""
    p1=""

    p0+="%F{blue}%n%f "
    p0+="at %F{cyan}%m%f "
    p0+="$(prompt_ooxx_git)"
    # p0+="$(prompt_ooxx_pyvenv)"
    # p0+="$(prompt_ooxx_nvm)"
    p0+="in %F{red}%~%f "
    p0+="%(?,,[%F{red}%B%?%b%f] ) "
    p0+="%(1j,[%j running] ,) "

    if (( $EUID == 0 )); then
        p1+="%F{magenta}#%f "
    else
        p1+="%F{magenta}❯%f "
        # p1+="%F{magenta}λ%f "
    fi

    PROMPT=$'$p0\n$p1'
}

prompt_ooxx_setup() {
    autoload -Uz vcs_info
    autoload -Uz add-zsh-hook
    add-zsh-hook precmd prompt_ooxx_precmd

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' use-simple true
    zstyle ':vcs_info:*' max-exports 2
    zstyle ':vcs_info:git*' formats '%b' 'x%R'
    zstyle ':vcs_info:git*' actionformats '%b|%a' 'x%R'
}

autoload -Uz promptinit
promptinit
prompt_themes+=( ooxx )
