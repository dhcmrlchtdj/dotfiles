_source() {
	if [[ -f $1 ]]; then
		source $1
	fi
}


export XDG_CONFIG_HOME="${XDG_CONFIG_HOME:-$HOME/.config}"
export XDG_CACHE_HOME="${XDG_CACHE_HOME:-$HOME/.cache}"
export XDG_DATA_HOME="${XDG_DATA_HOME:-$HOME/.local/share}"
export XDG_STATE_HOME="${XDG_STATE_HOME:-$HOME/.local/state}"
export PATH="$HOME/.local/bin:$PATH"
export LANG='en_US.UTF-8'
export LC_COLLATE='C'
export GPG_TTY=$(tty)
export EDITOR='nvim'
export LESS='-FXSR'
export LESS_TERMCAP_mb=$'\E[01;31m'
export LESS_TERMCAP_md=$'\E[01;31m'
export LESS_TERMCAP_me=$'\E[0m'
export LESS_TERMCAP_se=$'\E[0m'
export LESS_TERMCAP_so=$'\E[01;44;33m'
export LESS_TERMCAP_ue=$'\E[0m'
export LESS_TERMCAP_us=$'\E[01;32m'


setopt no_beep
setopt no_sh_word_split
# changing directories
setopt auto_cd
# setopt auto_pushd
# setopt pushd_ignore_dups
# setopt pushd_minus
# completion
setopt complete_in_word
setopt hash_list_all
setopt menu_complete
# expansion and globbing
setopt extended_glob
setopt glob_dots
setopt unset
# history
export HISTFILE="${XDG_STATE_HOME}/zsh/history"
export HISTSIZE=1000000
export SAVEHIST=1000000
setopt extended_history
setopt inc_append_history
setopt hist_ignore_dups
setopt hist_ignore_space
# input/output
setopt correct
setopt interactive_comments
# job control
setopt no_bg_nice
setopt no_hup
setopt long_list_jobs
setopt notify

source "${XDG_CONFIG_HOME}/zsh/git-prompt.sh"
source "${XDG_CONFIG_HOME}/zsh/zshrc_prompt_ooxx"
prompt ooxx

export _Z_DATA="${XDG_STATE_HOME}/zsh/z.sh"
source "${XDG_CONFIG_HOME}/zsh/z.sh"


### alias ###
alias ...='cd ../../'
alias sudo='sudo '
alias cp='cp -nvr'
alias mv='mv -nv'
# alias ls='ls --group-directories-first -vF'
alias ls='exa --group-directories-first -aF'
alias la='ls'
alias ll='ls -l -g -h --git --time-style=long-iso'
alias lle='ll --extended'
alias lll='ll --tree -I ".git"'
alias du='du -h -d 1'
alias df='df -P -h'
alias free='free -h'
alias ffmpeg='ffmpeg -hide_banner'
# alias tree='tree -F --dirsfirst -v'
# alias sc='screen -dRR'
alias sc='tmux new -As0'
alias scsc='tmux -CC'
alias tmuxcap='tmux capture-pane -p -S - | nvim -'

# pacman -S trash-cli
# apt install trash-cli # ~/.local/share/Trash
# brew install macos-trash
alias rm='rm -irv'

alias j='jobs -dl'
alias h='htop'
alias f='find . -name'
alias vim='nvim'
alias vimdiff='nvim -d'
alias rg='rg --smart-case --hidden -g "!{.git}"'

alias zz='bsdtar -cavf'
alias xx='unar'
# alias sss='python3 -m http.server'
# alias mm='mitmproxy -p 8888'
# alias mmm='mitmproxy -p 8888 -i "~q | ~s"'
# alias ccc='clang -std=c11 -Weverything -Wno-missing-prototypes'
### END alias ###


# title
function set_title() {
	echo -ne "\e]0;${(%):-"%n @ %m : %~"}\a"
}
autoload -Uz add-zsh-hook
add-zsh-hook -Uz precmd set_title

# emacs keymap
bindkey -e

# search history
autoload -Uz up-line-or-beginning-search
autoload -Uz down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey '^[[A' up-line-or-beginning-search # Up
bindkey '^[[B' down-line-or-beginning-search # Down
bindkey '^[OA' up-line-or-beginning-search # Up
bindkey '^[OB' down-line-or-beginning-search # Down
bindkey '^R' history-incremental-pattern-search-backward
bindkey '^S' history-incremental-pattern-search-forward


# completion
autoload -Uz compinit
compinit -d "${XDG_CACHE_HOME}/zsh/zcompdump-${ZSH_VERSION}"

# cache
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path "${XDG_CACHE_HOME}/zsh/zcompcache"
# completer
zstyle ':completion:*' completer _complete _match _correct _approximate _files
# allow one error for every three characters typed in approximate completer
zstyle ':completion:*:approximate:' max-errors 'reply=( $((($#PREFIX+$#SUFFIX)/3 )) numeric )'
# match uppercase from lowercase
zstyle ':completion:*:complete:*' matcher-list '' 'm:{a-z}={A-Z}'
zstyle ':completion:*:files:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# style
zstyle ':completion:*:descriptions' format '%F{red}completing %B%d%b%f'
zstyle ':completion:*:corrections' format '%F{red}%d (errors: %e)%f'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format '%F{red}No matches for:%f %d'
# menu
zstyle ':completion:*' menu select
# separate matches into groups
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-prompt ''
zstyle ':completion:*' select-prompt ''
# list style
# https://the.exa.website/docs/colour-themes
zstyle ':completion:*' list-colors 'fi=0:di=1;34:ln=36:or=4;31:pi=33:bd=1;33:cd=1;33:so=1;31:ex=1;32'
zstyle ':completion:*' list-dirs-first true
# ignore
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'
# manpage
zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.*' insert-sections true
# ssh
zstyle -e ':completion:*' hosts 'reply=()'
zstyle -e ':completion:*:(ssh|scp|rsync):*:hosts' hosts 'reply=(
$(grep -E "^Host[[:blank:]]+[a-z]" ~/.ssh/config 2>/dev/null | sed -E "s/Host[[:blank:]]+//")
)'
zstyle ':completion:*:(ssh|scp|rsync):*:users' ignored-patterns '_*'
zstyle ':completion:*:(ssh|scp|rsync):*:hosts' ignored-patterns '<->.<->.<->.<->'

###

function zzz() {
	for i in "$@"; do
		zz "${i}.zip" "${i}";
	done
}
